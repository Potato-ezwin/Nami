plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
}

group = "me.kiriyaga"
archivesBaseName = "nami"

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
        ignoreExitValue true
    }
    return stdout.toString().trim()
}

version = "2.3.9_10.${getGitHash()}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven {
        url = uri("https://api.modrinth.com/maven")
        content {
            includeGroup("maven.modrinth")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.21.5"
    mappings "net.fabricmc:yarn:1.21.5+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.128.1+1.21.5"
    modImplementation("maven.modrinth:sodium:mc1.21.5-0.6.13-fabric") {transitive = false}
    implementation "org.osgi:org.osgi.core:6.0.0"
}

loom {
    accessWidenerPath = file("src/main/resources/META-INF/nami.accesswidener")
}

tasks.processResources {
    inputs.property("version", project.version)
    filesMatching("fabric.mod.json") {
        expand("version": project.version)
    }
}
